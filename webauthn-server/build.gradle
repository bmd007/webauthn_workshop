plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
}

java {
	sourceCompatibility = 21
}

group = 'wonderland'

ext.buildNumber = {
	def buildNumber = System.getenv('VERSION')
	if (buildNumber == null || buildNumber.allWhitespace) {
		buildNumber = 'beta'
	}
	return buildNumber
}

project.version = buildNumber()

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('springCloudVersion', "2023.0.2")
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.hibernate.validator:hibernate-validator'
	compileOnly 'org.projectlombok:lombok'
	implementation 'com.google.guava:guava:33.2.1-jre'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.yubico:webauthn-server-core:2.6.0-alpha6'
	implementation 'com.yubico:webauthn-server-attestation:2.6.0-alpha6'
	implementation 'com.yubico:yubico-util:2.6.0-alpha6'
	implementation 'com.upokecenter:cbor:4.5.4'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('wrapper') {
	distributionType = Wrapper.DistributionType.ALL
}

bootBuildImage {
	imageName = "bmd007/${project.name}"
//	environment = ["BP_JVM_VERSION": "20"]
}
